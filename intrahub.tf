variable "intrahub_interface" {
  type = object({
    name     = string
    local_gw = string
    nat_ip   = string
    }
  )
  default = null
}

variable "intrahub_hubs" {
  description = "Set of intra-regional hub objects."
  type = set(object({
    hub_id    = string
    remote_gw = string
    # bgp_as      = number
    remote_ip = string
    local_ip  = string
    }
  ))
  default = null
}

locals {
  intrahub_hubs = var.intrahub_hubs == null ? {} : {
    for hub in var.intrahub_hubs : hub.remote_gw => hub
  }
}

resource "fortios_vpnipsec_phase1interface" "intrahub_phase1" {
  for_each = local.intrahub_hubs

  vdomparam = var.vdom

  name                     = "${var.hub_id}-to-${each.value.hub_id}"
  local_gw                 = var.intrahub_interface.local_gw == "" ? null : var.intrahub_interface.local_gw
  type                     = "static"
  interface                = var.intrahub_interface.name
  ike_version              = 2
  peertype                 = "any"
  net_device               = "disable"
  proposal                 = var.ipsec_proposal
  add_route                = "disable"
  dpd                      = "on-idle"
  auto_discovery_sender    = "enable"
  auto_discovery_forwarder = "enable"
  auto_discovery_receiver  = "enable"
  tunnel_search            = "nexthop" # removed in 7.0.x+
  psksecret                = var.ipsec_psk
  dpd_retryinterval        = 5
  remote_gw                = each.value.remote_gw
}

resource "fortios_vpnipsec_phase2interface" "intrahub_phase2" {
  for_each = local.intrahub_hubs

  vdomparam = var.vdom

  name       = fortios_vpnipsec_phase1interface.intrahub_phase1[each.key].name
  phase1name = fortios_vpnipsec_phase1interface.intrahub_phase1[each.key].name
  proposal   = fortios_vpnipsec_phase1interface.intrahub_phase1[each.key].proposal
  pfs        = "enable"
  dhgrp      = var.ipsec_dhgrp
}

resource "fortios_system_interface" "intrahub_vpn_interface" {
  for_each = local.intrahub_hubs

  autogenerated = "auto"

  description = "Managed by Terraform."
  ip          = each.value.local_ip
  name        = fortios_vpnipsec_phase1interface.intrahub_phase1[each.key].name
  remote_ip   = each.value.remote_ip
  allowaccess = "ping"
  vdom        = var.vdom
}

# resource "fortios_routerbgp_neighbor" "intrahub_neighbor" {
#   for_each = local.intrahub_hubs

#   vdomparam = var.vdom

#   ip                    = split("/", fortios_system_interface.intrahub_vpn_interface[each.key].remote_ip)[0]
#   remote_as             = each.value.bgp_as
#   attribute_unchanged   = "next-hop"
#   ebgp_enforce_multihop = "enable"
#   update_source         = fortios_system_interface.intrahub_vpn_interface[each.key].name
#   soft_reconfiguration  = "enable"
#   capability_graceful_restart = "enable"
#   next_hop_self         = "disable"
# }

resource "fortios_routerospf_ospfinterface" "intrahub_neighbor" {
  for_each = local.intrahub_hubs

  vdomparam = var.vdom

  interface = fortios_system_interface.intrahub_vpn_interface[each.key].name
  # ip = split("/", fortios_system_interface.intrahub_vpn_interface[each.key].remote_ip)[0]
  name = fortios_system_interface.intrahub_vpn_interface[each.key].name
}

resource "fortios_routerospf_network" "intrahub_network" {
  for_each = local.intrahub_hubs

  vdomparam = var.vdom

  prefix = fortios_system_interface.intrahub_vpn_interface[each.key].ip
  area   = "0.0.0.0"
}